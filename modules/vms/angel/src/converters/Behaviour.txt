shared abstract class Behaviour : IBehaviour {
    void start() {
    }

    void update() {
    }

    IBehaviour @getObject(AngelBehaviour @behaviour) {
        if(behaviour !is null) {
            return behaviour.scriptObject();
        }
        return null;
    }

    Actor @actor() {
        if(_root is null) {
            Actor @super = cast<Actor>(Engine::objectCreate("Actor"));
            @_root = cast<AngelBehaviour>(super.addComponent("AngelBehaviour"));
            _root.setScriptObject(@this);
        }
        return _root.actor();
    }

    Scene @scene() {
        return _root.scene();
    }

    World @world() {
        return _root.world();
    }

    Transform @transform() {
        return _root.transform();
    }

    Component @component(const string type) {
        return _root.component(type);
    }

    Actor @instantiate(Prefab @prefab, Vector3 position, Quaternion rotation) {
        return _root.instantiate(prefab, position, rotation);
    }

    string tr(const string source) {
        return _root.tr(source);
    }

    void deleteLater() {
        _root.deleteLater();
    }

    string _SIGNAL(const string name) {
        return "1" + name;
    }

    string _SLOT(const string name) {
        return "2" + name;
    }

    bool connect(Object @sender, const string &in signal, Object @receiver, const string &in slot) {
        return _connect(sender, signal, receiver, slot);
    }

    bool connect(Object @sender, const string &in signal, Behaviour @receiver, const string &in slot) {
        return _connect(@sender, signal, cast<Object>(receiver._root), slot);
    }

    bool connect(Behaviour @sender, const string &in signal, Behaviour @receiver, const string &in slot) {
        return _connect(cast<Object>(sender._root), signal, cast<Object>(receiver._root), slot);
    }

    bool connect(Behaviour @sender, const string &in signal, Object @receiver, const string &in slot) {
        return _connect(cast<Object>(sender._root), signal, receiver, slot);
    }

    private AngelBehaviour @_root;
};
